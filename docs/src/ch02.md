
# [第2回：▼ 複数のプロットを描く](@id ch02)

## ■ リテラル

リテラル（literal）とは，
「文字の並び」の通りに解釈される量をいう．

### 数字リテラル
`1` や `1.1` はリテラルである．
それぞれ，整数 `1` ，小数 `1.1` という値として評価される．

!!! note
    計算機の中の小数は[■ 浮動小数点数](@ref)と呼ばれ，筆算の小数とは異なる．


### 文字列リテラル

文字列とは，「文字の並び」として表される量である．
文字列のリテラルは，「二重引用符（double quotation mark） `"` で囲まれた文字の並び」である．

```@repl
"Hello world"
```

文字列を連結するには，演算子 `*` を用いる．
```@repl
h = "Hello"
w = "world"
h * w
h * " " * w
```

!!! note
    演算子 `*` は，数どうしに対して用いると乗算の意味になる．文字列どうしに用いると文字列の連結の意味になる．このように，同じ演算子 `*` に対して，複数の意味があり，適用する値の型（の組合せ）に応じて，適切な意味が選ばれて，計算される．

数字を表す文字列を作るには，`string` 関数を用いる．
```@repl
string(0)
string(1)
string(1.1)
```

特定の書式で文字列を作る場合には，`@sprintf` マクロを用いる．→ [▶ 書式付き文字列と出力](@ref) 


### シンボル

コロン `:` から始まる空白以外の文字の並びは，シンボルと呼ばれる固定の文字列である．文字列リテラルよりも効率よく処理される．これから説明するキーワード引数について，特別な意味を持つ値としても用いられる．


### 論理値 : `true` と `false` 

`true` と `false` は，条件が成立すること（真），または，成立しないことを表すリテラルである．この先の [■ 論理型](@ref) で詳しく説明する．


## ■ プロット全体の見かけの調整

プロットを描画するためには，
寸法，線幅，色，フォントの種類と大きさなど，
様々な属性（properties）を指定する必要がある．
プロットごとに，すべての属性を指定するのは大変なので，
何も指定されなかった属性は，
あらかじめ用意した設定や値を使うことになっている．

!!! note
    あらかじめ用意した設定や値を，既定値（default values）という．既定値を使わず，設定や値を個別に指定することを，既定値を「上書き（override）する」ともいう．

以下の節では，プロットのいくつかの属性を変更する方法を紹介する．

### ▼ 全体に太くする

これまで描いてきたプロットは，軸やデータ系列の線が細く，文字が小さい．プロット全体を太くするには，`thickness_scaling=数字` を指定するとよい．

★ `thickness_scaling=` を指定しない場合

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide

plot() # <= 単に枠を描くだけ
xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, xs)
```

★ `thickness_scaling=2` を指定した場合

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2) # <=

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, xs)
```

!!! note
    関数を呼び出すときに渡す値を，引数（ひきすう）または実引数（じつひきすう，argument）という．`thickness_scaling=数字` のように名前付きで渡す引数を，キーワード引数（keyword argument）という．


この指定は，既存のプロット・オブジェクトに対しても指定できる．関数 `plot!` を用いる．

★ データ系列を描いた後に指定する

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot()

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, xs)
plot!(thickness_scaling=2) # <=
nothing #hide
```
(図は同じ．省略)

### ▼ 箱型にする

縦軸と横軸を右と上に複製して「箱」のように見せるには，属性 `framestyle=:box` を指定する．（`:box`は[シンボル](@ref)である）

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2)
plot!(framestyle=:box) # <= 二つ目以降は plot! 関数

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, xs)
```

関数 `plot` または `plot!` 関数に与える
キーワード引数は，順番を問わない．
また，複数のキーワード引数を，
一つの `plot` または `plot!` 関数に書いてもよい．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box) # <=

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, xs)
nothing #hide
```
(図は同じ．省略)


## ■ 軸の見かけの調整

- [▼ 軸にテキストを付与する](@ref)
- [▼ プロットの描画範囲を指定する](@ref)
- [▼ アスペクト比を等しくする](@ref)
- [▼ グリッド線を消去する](@ref)

### ▼ 軸にテキストを付与する

既定では，横軸と縦軸には，テキストは表示されない．

横軸・縦軸に，テキストを付与するには，
キーワード引数 `xlabel=文字列` と `ylabel=文字列`を用いる．

- `xlabel=文字列` : 横軸のテキストを指定
- `ylabel=文字列` : 縦軸のテキストを指定

★ データ系列を描画する前に，軸テキストを指定

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box)
plot!(xlabel="x") # <=
plot!(ylabel="y") # <=

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, xs)
```

★ データ系列を描画した後に，軸テキストを指定

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box)

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, -1 * xs)
plot!(xlabel="x", ylabel="y")  # <= 1行にまとめた
nothing #hide
```
(図は同じ．省略)


属性 `xlabel=`，`ylabel=` を指定する代わりに，
関数 `xlabel!(文字列)`と関数 `ylabel!(文字列)` を用いてもよい．


```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box)

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xs, xs)
xlabel!("x")
ylabel!("y")
nothing #hide
```
(図は同じ．省略)


### ▼ プロットの描画範囲を指定する

横軸，縦軸の描画範囲を指定するには，
`plot`または`plot!`関数で，
キーワード引数 `xlims=` と `ylims=` を用いる．
- `xlims=(左限, 右限)`
- `ylims=(下限, 上限)`


上のプログラムに続けて

```@example ch002
# 描画範囲
plot!(xlims=(-0.5, 0.5)) # <=
plot!(ylims=(-0.5, 0.5)) # <=
```


既に描いたプロットオブジェクトの描画範囲を変更するなら，
属性 `xlims=`，`ylims=` を指定する代わりに，
関数 `xlabel!(文字列)`と `ylabel!(文字列)` を用いてもよい．

- `xlims!(左限, 右限)`
- `ylims!(下限, 上限)`

上のプログラムに続けて

```@example ch002
# 描画範囲
xlims!(-2, 2) # <=
ylims!(-2, 2) # <=
```


### ▼ アスペクト比を等しくする

ここまで示したプロットでは，直線 `y=x` と `y=-x` を描画した．
- `y=x` は傾き 1（斜め45°で増加）の直線
- `y=-x` は傾き -1（斜め45°で減少）の直線
であるが，これまで描いたプロットでは，それらの傾きで描画されていない．これは，縦横の寸法が異なるためである．
「単位長さを表す縦横の寸法の比」をアスペクト比（aspect ratio）という．

アスペクト比を指定するには，
キーワード引数 `aspect_ratio=値` を用いる．

既定値は `aspect_ratio=:auto` であり，アスペクト比を適当に調節する．

「アスペクト比を等しく」するには，`aspect_ratio=:equal` または`aspect_ratio=1` を指定する．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box)
plot!(aspect_ratio=:equal) # <=

xs = -1:0.2:1 
plot!(xs, -1 * xs)
plot!(xs, xs)
```

直線 `y=x` と `y=-x` が正しい傾きで描画できた．

「アスペクト比を等しく」しても，上の結果のように
縦横の描画範囲が等しい軸（正方形）が設定されるとは限らない．
必要なら，描画範囲を追加で設定する．

```@example ch002
# 上に続けて
xlims!(-1.2,1.2)
ylims!(-1.2,1.2)
plot!() #hide
```

!!! note
    アスペクト比は，横軸全体の寸法に対する縦軸全体の寸法ではない．

今度は，横 `2`，縦 `1` の枠を描画してみよう．
「アスペクト比が等しい」ので，正しく
横 `2` ，縦 `1` の長方形の枠が描画された．


```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box)
plot!(aspect_ratio=:equal) # <=
xlims!(0,2)
ylims!(0,1)
plot!(xs,xs)
plot!(xs,1 .- xs)
plot!() #hide
```


### ▲ 練習：三角形の拡大縮小・平行移動を描画する

※ この練習は，本節を読み終えてから着手しても良い．

点を結んで図形を書く場合は，
アスペクト比を等しくする必要がある．
さもないと，図形が歪になる．

第1回の[■ ベクトルをスカラーで乗除する](@ref)と[■ ベクトルをスカラーで加減する](@ref) では，
三角形を拡大縮小したり，平行移動したりした．それぞれの操作を行った三角形を同じ描画範囲で描け．
確かに拡大縮小または平行移動となっていることを観察せよ．
解答例 → [■ ベクトルに対する更新演算：乗除](@ref)，[■ ベクトルに対する更新演算：加減](@ref)


### ▼ グリッド線を消去する

属性 `grid` は，グリッド線の描画を指定する．

すべてのグリッド線を消去するには，
属性 `grid=false` を指定すればよい．


```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box,
aspect_ratio=:equal, 
xlims=(-1,1), ylims=(-1,1))

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(grid=false)
```

横軸，縦軸のグリッド線の有無を指定するには，
属性 `xgrid`，`ygrid` を用いる．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(thickness_scaling=2, framestyle=:box,
aspect_ratio=:equal, 
xlims=(-1,1), ylims=(-1,1))

xs = -1:0.1:1
plot!(xs, -1 * xs)
plot!(xgrid=true, ygrid=false)
```


## ■ 凡例を調整する

### ▼ データ系列の凡例テキストを指定する

データ系列を区別するための説明書きを，
プロットの凡例（はんれい，legend）という．

何も指定しなければ，
各データ系列に `y1`，`y2`, `y3` というラベルが順番に付与される．

各データ系列に，独自のラベルを付与したい場合には，
データ系列に続けて，キーワード引数 `label=文字列` を指定する．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(
   thickness_scaling=2, framestyle=:box,
   aspect_ratio=:equal)

xs = -3:0.1:3
plot!(xs, -1 * xs, label = "y=-x")
plot!(xs, xs, label = "y=x")
plot!(xlims=(-3,3),ylims=(-3,3))
```

特定のデータ系列の凡例を表示させないようにするには，
`label`に空文字列を指定する `label=""`する．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(
   thickness_scaling=2, framestyle=:box, 
   aspect_ratio=:equal,
   xlabel="x", ylabel="y")

xs = -3:0.1:3
plot!(xs, -1 * xs, label = "") # <=
plot!(xs, xs, label = "y=x") # <=
plot!(xlims=(-3,3),ylims=(-3,3))
```


### ▼ 凡例の位置を調整する

キーワード引数 `legend` は，凡例の位置を指定するのにも用いられる．
既定値は `legend=:best` であり，
データ系列となるべく干渉しない位置に凡例を配置する．

例えば，凡例をプロットの下側に配置するには，
`legend=:bottom` と指定する．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(
   thickness_scaling=2, framestyle=:box,
   aspect_ratio=:equal)

xs = -3:0.1:3
plot!(xs, -1 * xs, label = "y=-x")
plot!(xs, xs, label = "y=x")
plot!(legend=:bottom) # <=
plot!(xlims=(-3,3),ylims=(-3,3))
```

`legend` 属性の取りうる位置の値は，以下の通りである．

- :right 右
- :left 左
- :top 上中央
- :bottom 下中央
- :inside 中央
- :topleft 左上
- :topright 右上
- :bottomleft 左下
- :bottomright 右下
- :outtoprint 枠外（利用できない場合がある）

### ▼ 凡例を透明にする

凡例を透明にして枠を消去するには，以下の二つの属性を指定する

- `legend_foreground_color=nothing`
- `legend_background_color=nothing`

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(
   thickness_scaling=2, framestyle=:box,
   aspect_ratio=:equal)
plot!(
   legend=:bottom,
   legend_foreground_color=nothing, # <=
   legend_background_color=nothing) # <=
xs = -3:0.1:3
plot!(xs, -1 * xs, label="y=-x")
plot!(xs, xs, label="y=x")
plot!(xlims=(-3,3),ylims=(-3,3))
```

### ▼ 凡例を非表示にする

すべてのデータ系列の凡例を消去するには，
キーワード引数 `legend=false` を指定する．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(
   thickness_scaling=2, framestyle=:box,
   aspect_ratio=:equal)

plot!(legend=false) # <=
xs = -3:0.1:3
plot!(xs, -1 * xs) 
plot!(xs, xs)
plot!(xlims=(-3,3),ylims=(-3,3))
```

## ▼ 水平線・垂直線を加える

式 $y=ax$ は，すべて原点 $(0,0)$ を通る．
水平線や垂直線の補助線を引いて，これを見やすくしよう．

関数 `hline!(ys)` は，水平線（horizontal line）を追加する．
引数 `ys` として，水平線を引く $y$ 座標のコレクションを指定する．

また，関数 `vline!(xs)` は，垂直線（vertical line）を追加する．
引数 `xs` として，垂直線を引く $x$ 座標のコレクションを指定する．

今までのプロットに，y軸 `x=0` とx軸 `x=0` を黒い細線で追加しよう．

- `linewidth=数字` は，線幅（linewidth）を指定する．
※ 別名 `lw=数字`
- `linecolor=:black` は，黒色（black）で描くことを指定する
※ 別名 `lcolor=色`，`lc=色`

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(
   thickness_scaling=2, framestyle=:box,
   aspect_ratio=:equal)

plot!(legend=false) # <=
xs = -3:0.1:3
plot!(xs, -1 * xs)
plot!(xs, xs)
hline!([0], color = :black, linewidth = 0.5) # <=
vline!([0], color = :black, linewidth = 0.5) # <=
plot!(xlims=(-3,3),ylims=(-3,3))
```


## ▼ プロットの既定値を指定する

関数 `plot` または `plot!()`のキーワード引数として，これまでに，以下を紹介した．

- `thickness_scaling=数字`
- `framestyle=:box`
- `xlabel=文字列`
- `ylabel=文字列`
- `xlims=(左限, 右限)`
- `ylims=(下限, 上限)`
- `aspect_ratio=値`
- `grid=trueまたはfalse`
- `xgrid=trueまたはfalse`
- `ygrid=trueまたはfalse`


これらの指定は，新しいプロットオブジェクトを作成するたびに行う必要がある．

`default` 関数を用いると，
プロットの既定値を変更できる．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
default(
   thickness_scaling=2, framestyle=:box,
   aspect_ratio=:equal, 
   xlims=(-1,1), ylims=(-1,1))

xs = -1:0.1:1
plot()
plot!(xs, -1 * xs)
plot!(xs, xs)
```

新しいプロット・オブジェクトを`plot()` 関数で作ったとき，関数 `default()` 関数で設定した既定値が用いられる．

```@example ch002
plot(xs, xs)
```

`default` 関数で設定したプロットの既定値を元に戻すには `Plots.reset_defaults()`を用いる．

```
Plots.reset_defaults()
```

## プロットデフォルトの設定2

この章の，ここから下は，以下のような既定値を設定しよう．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
Plots.reset_defaults()

default(
   thickness_scaling=2, framestyle=:box,
   aspect_ratio=:auto, 
   legend_foreground_color=nothing,
   legend_background_color=nothing,
   xlabel="x", ylabel="y")
```


## ■ `for` 文

[Repeated Evaluation: Loops (section)](https://docs.julialang.org/en/v1/manual/control-flow/#man-loops) ([日本語訳](https://atelierarith.github.io/UnofficialJuliaDoc-ja/manual/control-flow.html#man-loops))

`for` 文を用いると，[■ コレクション](@ref)
から要素を一つづつ取り出して，
`end` 文が出現するまでの文を繰り返して，計算を行うことができる．
この繰り返される部分をブロック（block）という．
ブロックは，字下げ（indent）で表記される．
が，字下げは見やすさのためだけである．

繰り返しをループ（loop）ともいう．

!!! note
    ブロックは，`for` 文以外にも出現する（たとえば，[■ if文](@ref)）．
    区別する場合は `for` ブロックとも称する．

次の例では，変数 `i` にベクトルの各要素を入れて，`end` 文までの計算を繰り返す．
コレクションの各要素が入る変数をループ変数（loop variable）という．

```@repl
# @highlight-start[1]
for i in [1, 3, 2]
   @show i   # 式 i の値を表示する
end # @highlight-end
```

`@show i` は，式 `i` の値を表示するマクロである．

範囲を用いた `for` 文の例を示す．

`println(x)` 関数は，`x` を印字してから，改行する命令である．

```@repl
# @highlight-start[1]
for i = 1:5
   println(string(i))
end # @highlight-end
```

```@raw comment

## ■ 内包表記

[Comprehensions (section)](https://docs.julialang.org/en/v1/manual/arrays/#man-comprehensions) ([日本語訳](https://atelierarith.github.io/UnofficialJuliaDoc-ja/manual/arrays.html#man-comprehensions))

[ 2x for x in [1:2] ]
```

## ▼ for 文でパラメータを変えて，複数のプロットを描く


比例関係 $y = ax$（ただし $a = 1, 2, 3, 4, 5$ ）のプロットを描こう．

```@example ch002
plot(aspect_ratio=:equal,legend=:bottomright)
xs = -4:0.1:4
plot!(xs, xs, label = "y= x")
plot!(xs, 2 * xs, label = "y=2x")
plot!(xs, 3 * xs, label = "y=3x")
plot!(xs, 4 * xs, label = "y=4x")
plot!(xs, 5 * xs, label = "y=5x")
plot!(xlims=(-4,4),ylims=(-4,4))
```

データ系列を描画する部分は，ほぼ同じ文を書いている．
「繰り返し」を用いて，ほぼ同じ文をまとめよう．

次の例の `for` 文では，
ループ変数 `a` に，1, 2, 3, 4, 5 の値を順番に入れて，`end` までの文を実行する．
ここで，繰り返しの中で `xs` は書き換えられないから，`xs` への代入は繰り返しの前に一度だけ行えばよい．


```@example ch002
plot(aspect_ratio=:equal,legend=:bottomright)
xs = -4:0.1:4

# @highlight-start[1]
for a = 1:5
   plot!(xs, a * xs)
end # @highlight-end
plot!(xlims=(-4,4),ylims=(-4,4))
```

次に，`label=文字列` に与える文字列を，`for`文を用いて作ってみよう．


```@example ch002
# @highlight-start[1]
for a = 1:5
   label1 = "y=" * string(a) * "x"
   println(label1)
end # @highlight-end
```

各データ系列の `label` に，上で計算された文字列を与えればよい．

```@example ch002
plot(aspect_ratio=:equal,legend=:bottomright)
xs = -4:0.1:4

# @highlight-start[1]
for a = 1:5
   label1 = "y=" * string(a) * "x"
   plot!(xs, a * xs, label=label1)
end # @highlight-end
plot!(xlims=(-4,4),ylims=(-4,4))
```

## ▲ 練習

さらに補助線 $x = 0$ と $y = 0$ を追加せよ．

## ▼ 冪乗関数を描く

[`Base.:^` - Method](https://docs.julialang.org/en/v1/base/math/#Base.:^-Tuple{Number,%20Number}) ([日本語訳](https://atelierarith.github.io/UnofficialJuliaDoc-ja/base/math.html#Base.:^-Tuple{Number,%20Number}))

`x^y` は，冪（べき，power）ないし冪乗（べきじょう）$x^{y}$ を表す．
$x$ を底（base），$y$ を冪指数（exponent）という．

```@repl
2^2
2^3
2^4
```

スカラー `c` とベクトル `v` に演算子 `.^` を適用した式 `c .^ v` は，
各々の冪指数に対して冪乗した値を要素とするベクトルを与える．

底が整数の場合は `.^` の前に空白を入れる．

```@repl
2 .^[2,3,4]
```

!!! note
    整数（底）の直後に `.^` と書くと例外が出るので注意しよう．

    ```@jldoctest
    julia> 2.^[2,3,4]
    ERROR: syntax: invalid syntax "2.^"; add space(s) to clarify
    ```

底が小数の場合は，その直後に `.^` と書いてよい．

```@repl
2.0.^[2, 3, 4]
```

ベクトルとスカラーに演算子 `.^` を適用した式 `v .^ c` は，
各々の底に対して冪乗した値を要素とするベクトルを与える．

```@repl
[2, 3, 4] .^ 2
[2, 3, 4] .^ 2
```

範囲 `v` とスカラー `c` に演算子 `.^` を適用した式 `v .^ c` は，
各々の底に対して冪乗した値を要素とするベクトルを与える．

```@repl
(2:4) .^ 2
2:4 .^ 2    # `^`は `:` よりも優先度が高い
```

区間 $x= [0,1]$ で，冪乗 $y=x^a$（ただし $a = 2,3,4,5$ ）のプロットを描こう．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(aspect_ratio=:equal)

xs = 0:0.1:1
plot!(xs, xs .^ 2)
plot!(xs, xs .^ 3)
plot!(xs, xs .^ 4)
plot!(xs, xs .^ 5)

# 描画範囲を設定
xlims!(-0.05, 1.05)
ylims!(-0.05, 1.05)
```

`for` 文を使って，繰り返しの処理をまとめる．

`plt.axes().set_aspect("equal")` は，プロットを書く前に一度だけ実行する．

区間を $x= [0,2]$ に拡大して，凡例を追加しよう．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(aspect_ratio=:equal)

xs = 0:0.05:2
# @highlight-start[1]
for a = 2:5
   plot!(xs, xs .^ a, label = "y=x^" * string(a))
end # @highlight-end

# 描画範囲を設定
xlims!(-0.05, 2)
ylims!(-0.05, 2)
```

冪乗 $y=x^a$ （ただし $a = 2, 3, 4, 5 $）は，すべて点 $(1,1)$ を通る．
直線 $x = 1$ と $y = 1$ を付与して，これを見やすくしよう．


```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(aspect_ratio=:equal)

# 間隔を狭めた
xs = 0:0.05:2
# @highlight-start[1]
for a = 2:5
   plot!(xs, xs .^ a, label = "y=x^" * string(a))
end # @highlight-end
# 描画範囲を設定
xlims!(-0.05, 2)
ylims!(-0.05, 2)
# 水平線 y=1
hline!([1], color = :black, linewidth = 0.5, label="")
# 垂直線 x=1
vline!([1], color = :black, linewidth = 0.5, label="")
```

## ▼ ローレンツ関数を描く

[Lorentzian Function](http://mathworld.wolfram.com/LorentzianFunction.html)

以下の曲線を，
ローレンツ関数（Lorentzian function）という．

```math
\begin{gathered}
y = \dfrac{\dfrac{\gamma}{2}}{(x-x_0)^2+\left(\dfrac{\gamma}{2}\right)^2}, \\
\gamma > 0
\end{gathered}
```

パラメータを $x_0 = 0, \gamma = 2$ のように選ぶと，
以下のように簡単な形となる．

```math
y = \dfrac{1}{x^2+1}
```

まず，この曲線を描いてみる．

分母 $x^2+1$ を計算するには，「ベクトル」 `xs.^2` にスカラー `1` を加算する．
このとき，`+` の前にピリオド `.` を付与した
ドット演算子（dot operator）`.+` を用いる．

```@repl ch000
xs = -3:0.5:3
xs.^ 2 .+ 1
```

「スカラー」$1$ を「ベクトル」 $(xs.^2+1)$ の各要素で割る
には，`/` の前にピリオド `.` を付与したドット演算子 `./` を用いる．

```@repl ch000
1 ./ (xs.^ 2 .+ 1)
```

上のプロットを描こう．`xs` の刻みを小さくしておく．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot()

xs = -3:0.1:3
ys = 1 ./ (xs.^ 2 .+ 1)
plot!(xs, ys)
plot!(xlims=(-3,3),ylims=(0,1.2))
```

以下のように，パラメータ $\gamma$ を追加する．

```math
y = \dfrac{\dfrac{\gamma}{2}}{x^2+\left(\dfrac{\gamma}{2}\right)^2}
```

3つのパラメータ $\gamma=0.5, 1, 2$ について，この曲線を描く．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot()

xs = -3:0.05:3
gamma = 0.5
ys = (gamma / 2) ./ (xs.^ 2 .+ (gamma / 2)^2)
plot!(xs, ys, label = gamma)
gamma = 1.0
ys = (gamma / 2) ./ (xs.^ 2 .+ (gamma / 2)^2)
plot!(xs, ys, label = gamma)
gamma = 2.0
ys = (gamma / 2) ./ (xs.^ 2 .+ (gamma / 2)^2)
plot!(xs, ys, label = gamma)
plot!(xlims=(-2,2),ylims=(0,4.2))
```

`gamma` の値が変わっても，それぞれの曲線を描くための命令は変わらない．
`for` 文を用いて，`gamma` の値を変えてみよう（結果のプロットは前出のため省略）．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot()

xs = -3:0.05:3
# @highlight-start[1]
for gamma in [0.5, 1.0, 2.0]
   ys=(gamma / 2) ./ (xs.^ 2 .+ (gamma / 2)^2)
   plot!(xs, ys, label = gamma)
end # @highlight-end
plot!(xlims=(-2,2),ylims=(0,4.2))
```

!!! note
    本章のプログラムを順に打ち込みながら対話すると，変数 `ys` が定義済みである．上のコードの `for`ブロックの最初の文 `local ys` は，`for`ブロックの外で定義された（＝全域）変数 `ys` の値を変更するのではなく，`for`ブロック内部のみで定義される（＝局所）変数 `ys` を用いることをJuliaに指示する．Julia 1.6では，`local`を付けなくても上のプログラムは動作するが警告メッセージが表示される．


ローレンツ関数には，次の性質がある．
* 点 $x = 0$ で 最大値 $y = \dfrac{2}{\gamma}$
* 点 $x = \pm\dfrac{\gamma}{2}$ で，最大値の半分 $y = \dfrac{1}{2}\dfrac{2}{\gamma}$

2つ目の性質を観察するため，最大値に対する比を描いてみる．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot()

xs = -3:0.05:3
# @highlight-start[1]
for gamma in [0.5, 1.0, 2.0]
   ys=(gamma / 2) ./ (xs.^ 2 .+ (gamma / 2)^2) / (2 / gamma)
   plot!(xs,ys, label = gamma,
   )
end # @highlight-end
hline!( [ 1/2 ], color = :black, linewidth = 0.5, label="")
plot!(xlims=(-3,3),ylims=(0,1.2))
```

!!! note
    `plot` 関数の文は，複数の行にまたがっていることに注目したい．式や文が次の行に続くことを示す記法は，とくに用意されていない．構文が行末で終わらなければ，次の行まで読みにいく．

パラメータ $\gamma$ は，半値全幅（Full Width of Half Maximum, FWHM）と呼ばれる． $\gamma$ を非常に小さくすると，Diracのデルタ関数（Dirac delta function）の近似（の1つ）となる．




## ▼ 同じ寸法のコレクションどうしの加減

同じ寸法のコレクションに演算子 `+` または `-` を使うと，
対応する要素どうしの加減ができる．
[■ ドット演算子](@ref)は不要である．

・ベクトル（の対応する要素）どうしの加減算

```@repl
xs = [1, 2, 2, 1]
ys = [1, 1, 3, 1]
xs + ys
xs - ys
```

・範囲（の対応する要素）どうしの加減算

```@repl
xs = 100:100:300
ys = 1:2:5
xs + ys
xs - ys
```

結果も範囲となる（賢い！）．

・範囲とベクトル（の対応する要素どうし）の加減算

```@repl
xs = 100:100:300
ys = [1,3,5]
xs + ys
xs - ys
```

「範囲どうしの加算」の例と比較せよ．範囲 `1:2:5` とベクトル `[1,3,5]` は，実質同じ値であるが，範囲とベクトルの加減算はベクトルになる．



## ▼ 同じ寸法のコレクションどうしの乗除

同じ寸法を持つコレクションどうしに対して
ドット付きの `.*` または `./` を適用すると，
対応する要素どうしで乗除を行う．
要素の数が異なると，例外（exception, エラー）が発生する．

・ベクトル（の対応する要素）どうしの乗除算

```@repl
xs = [1, 2, 4, 3]
ys = [2, 1, 2, 1]
xs .* ys
xs ./ ys
```

結果が範囲で表せない場合には，
ベクトルに変換される．

```@repl
xs = 100:100:300
ys = [1,3,5]
xs + ys
xs - ys
```

!!! note
    コレクションどうしの乗算 `*` は，配列どうしの積という別の演算に対応する．
    
    参考 → [■ 行列とベクトルの積](@ref)
    ```@repl
    # 列ベクトルと行ベクトルの積 => 行列
    [2,3] * [4,5]'
    ```
    
    参考 → [■ 行列と行列の積](@ref)
    ```@repl
    # 行ベクトルと列ベクトルの積 => 内積
    [2,3]' * [4,5]
    ```

## ■ 更新演算子

[Updating operators](https://docs.julialang.org/en/v1/manual/mathematical-operations/#Updating-operators) ([日本語訳](https://atelierarith.github.io/UnofficialJuliaDoc-ja/manual/mathematical-operations.html#Updating-operators))

変数に加減乗除などを行って，元の変数に再代入する場合には，
更新演算子（updating operator）を用いるとよい．
たとえば，加算の更新演算子は`+`記号の直後に `=` の文字を付与する．

```@repl
x = 1
# 再代入
x = x + 1
# 更新演算子
x += 1
```

スカラー変数に対する演算子 `+`，`-`，`*`，`/`，`^` に対して，
更新演算子 `+=`，`-=`，`*=`，`/=`，`^=` が用意されている．

以下の例は，変数 `gamma` を 2 で繰り返し割り算する．

```@repl
gamma = 2
# @highlight-start[1]
for i = 1:5
   global gamma
   gamma /= 2
   @show gamma
end # @highlight-end
```

更新演算子 `/=` を利用して，ローレンツ関数のパラメータ `gamma` を変えてみる．

`for` 文の次の `global gamma` に注目してほしい．
対話型で実行する場合，`for` 文の外側で定義された変数の値を変更することは，
安全のために通常禁止されている．これを可能とするのが `global` 文である．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot()

xs = -3:0.05:3
gamma = 2
# @highlight-start[1]
for i = 1:5
   global gamma
   ys=(gamma / 2) ./ (xs.^ 2 .+ (gamma / 2)^2) / (2 / gamma)
   plot!(xs,ys, label = gamma)
   gamma /= 2
end # @highlight-end
hline!([1/2], color = :black, linewidth = 0.5, label="")
plot!(xlims=(-3,3),ylims=(0,1.2))
```

### ▲ 練習

ローレンツ関数において，パラメータ $x_0$ を変えたプロットを作成せよ．
`for` 文を使う方法，更新演算子を使う方法の，2つで描いてみよ．

### ■ ベクトルに対する更新演算：乗除

ベクトルを保持する変数に対しても，更新演算が用意されている．

・ベクトルを保持する変数を，スカラー倍して更新する

ベクトルをスカラー倍して更新するには，更新演算子 `*=` を用いる．
式 `v *= c` は，`v = v * c` と同じ動作をする．

```@repl
xs = [1, 2, 2, 1]
xs *= 2
xs
```

前節の三角形を描く例題で，更新演算子 `*=` を用いて，図形を繰り返し拡大してみよう．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(aspect_ratio=:equal)

xs = [1.0, 2.0, 2.0, 1.0]
ys = [1.0, 1.0, 3.0, 1.0]
# @highlight-start[1]
for i = 1:5
   global xs, ys
   plot!(xs, ys)
   xs *= 1.2
   ys *= 1.2
end # @highlight-end
xlims!(0, 7)
ylims!(0, 7)
```

ベクトルをスカラーで除して更新するには，
更新演算子 `/=` を用いる．
式 `v /= c` は，`v = v / c` と同じ動作をする．

### ■ ベクトルに対する更新演算：加減

ベクトルにスカラーを加減して更新するには，更新演算子 `.+=`または `.-=` を用いる．
式 `v .+= c` は，`v = v .+ c` と同じ動作をする．

```@repl
xs = [1, 2, 2, 1]
xs .+= 1
xs
```

前節の三角形を描く例題で，更新演算子 `.+=` を用いて，
図形を繰り返し並行移動してみよう．

`for` 文の中で，変数 `xs` と `ys` を更新するので，`global xs, ys` の文が必要である．

```@example ch002
using Plots
#gr() #pythonplot()
closeall() #hide
plot(aspect_ratio=:equal)

xs = [1.0, 2.0, 2.0, 1.0]
ys = [1.0, 1.0, 3.0, 1.0]
# @highlight-start[1]
for i = 1:5
   global xs, ys
   plot!(xs, ys)
   xs .+= 0.5
   ys .+= 0.5
end # @highlight-end
xlims!(0, 6)
ylims!(0, 6)
```


### ■ ベクトルとコレクションとの更新演算

ベクトルとコレクションに対する演算にも更新演算子が用意されている．
ベクトル `v` とコレクション `w` に対して，
式 `v .+= w` は，`v = v .+ w` と同じ動作をする．

```@repl
xs = [1, 2, 2, 1]
ys = [1, 1, 3, 1]
xs .+= ys
xs
xs .-= ys
xs
```

同様に，式 `v .*= w` は，`v = v .* w` と同じ動作をする．


```@repl
xs = [1, 2, 2, 1]
ys = [1, 1, 3, 1]
xs .*= ys
xs
xs ./= ys
xs
```


### ▲ 練習

ベクトルの要素どうしの乗算 `.*` と `for`文を用いて，いくつかの冪乗関数のプロットを描いてみよ．

さらに，ベクトルの要素どうしの乗算の代わりに，ベクトル要素の乗算と更新演算子 `*=` も使ってみよ．


## ★ 今回のまとめ

* 文字列
* プロット全体の見かけの調整
* 冪乗関数
* `for` 文
* ローレンツ関数
* 同じ寸法のコレクションどうしの加減乗除
* 更新演算子


