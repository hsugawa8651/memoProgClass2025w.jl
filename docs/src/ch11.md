
# [第11回：行列・線形代数](@id ch11)

## プロットデフォルトの設定11

```@example ch011
aux = (label="", lc=:black, lw=0.5)
using Plots
using Plots.PlotMeasures

Plots.reset_defaults()
default(
    size=(720,480),
    framestyle=:box,thickness_scaling=2,
    dpi=150,
    guidefontsize=10,
    tickfontsize=8,
    legendfontsize=8,
    aspect_ratio=:auto,
    label="",
    legend_foreground_color=nothing,
    legend_background_color=nothing,
    grid=false,xlabel="", ylabel="",
    palette=:Set1_9,
    linewidth=2,
    markershape=:none,
    markersize=2,
    markerstrokewidth=0,
    warn_on_unsupported=true)
```

```@raw comment
## ■ ベクトルに対する演算

ベクトルの演算としては，これまでに，以下を説明した．

- ベクトルのスカラー倍
- ベクトルどうしの和差
- ベクトルを引数とする演算
-- 絶対値
-- 要素の和
-- 要素の積
-- 要素の平均値，標準偏差
```

## ▶ ベクトルの線形結合からなる格子点

複数のベクトルの線形結合とは，
それらのベクトルのスカラー倍を加え合わせたものを，それらのベクトルの線形結合という．

二つのベクトル
$a_1=\begin{bmatrix} 1 \\\\ 0 \end{bmatrix}, a_2=\begin{bmatrix} 0 \\\\ 1 \end{bmatrix}$
の「整数」係数の線形結合による格子点を描く．

さらに，
$b_1=\begin{bmatrix} \dfrac{1}{2} \\\\ \dfrac{1}{2} \end{bmatrix}, b_2=\begin{bmatrix} \dfrac{1}{2} \\\\ -\dfrac{1}{2} \end{bmatrix}$
の「整数」係数の線形結合からなる格子点を重ねる．


```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)
plot(framestyle=:origin)

a1 = [1, 0];
a2 = [0, 1];
for m = -3:3, n = -3:3
   r = m * a1 + n * a2
   scatter!([r[1]], [r[2]], ms=4, lc=1, shape=:circle)
end

b1 = [1 / 2, 1 / 2];
b2 = [1 / 2, -1 / 2];
for m = -3:3, n = -3:3
   r = m * b1 + n * b2
   scatter!([r[1]], [r[2]], lc=2, shape=:circle)
end

xlims!(-3,3)
ylims!(-3,3)
plot!()
```

どちらも正方格子（cubic lattice）であるが，座標系の取り方が異なる．
基底 $b_1, b_2$ で張られる格子点は，
基底 $a_1, a_2$ で張られる格子点の中央の点も含んでいることが観察できる．

今度は，
$c_1=\begin{bmatrix} 1 \\ 0 \end{bmatrix}, c_2=\begin{bmatrix} -\dfrac{1}{2} \\ \dfrac{\sqrt{3}}{2} \end{bmatrix}$ で張られる格子点を描いてみる．
これは，六方格子（hexagonal lattice）と呼ばれる．

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)
plot!(framestyle=:origin)

c1 = [1, 0];
c2 = [-1 / 2, sqrt(3) / 2];
for m = -3:3, n = -3:3
   r = m * c1 + n * c2
   scatter!([r[1]], [r[2]], mc=3, shape=:circle)
end

xlims!(-3,3)
ylims!(-3,3)
plot!()
```


```@raw comment
### ▼ ベクトルのスカラー倍，和差
- 絶対値
- 随伴
- 転置
```


## ■ 平面ベクトルの内積

関数 `dot(a,b)` は，ベクトル `a` と `b` との「内積（inner product）」を返す．

中置き演算子 ` ⋅` を用いて `a⋅b` と書くこともできる．
「 `⋅` 」 は，バックスラッシュ `\` に続けて cdots と入力してから，TABキーを押すことによって入力できる．
かな漢字変換システムで入力できる「・」（中黒=なかぐろ）とは，別の文字である．

### ▶ 平面ベクトルどうしのなす角を求める

ベクトル $a$ と $b$の内積は，$a$ と $b$ のなす角$\theta$ を用いて，以下のように定義される．

```math
a\cdot b = \left\vert{a}\right\vert \left\vert{b}\right\vert \cos\theta
```

これから，$\theta$ を求めるには，次の式を用いればよい．

```math
\cos\theta = \dfrac{a\cdot b}{ \left\vert{a}\right\vert \left\vert{b}\right\vert }
```

また，内積の定義から，自分自身の内積は，ノルムの二乗であることも分かる．

```math
a\cdot a = \left\vert{a}\right\vert^2
```


### ▶ 例：ベクトルどうしのなす角度を求める

上で出てきたベクトルのうち，`a1, a2, c1, c2` のノルムは $1$ である．

```@repl ch011
using LinearAlgebra
#
a1 = [1, 0];
a2 = [0, 1];
b1 = [1 / 2, 1 / 2];
b2 = [1 / 2, -1 / 2];
c1 = [1, 0];
c2 = [-1 / 2, sqrt(3) / 2];
norm(a1)
norm(a2)
norm(c1)
norm(c2)
```

`b1` と `b2` のノルムは $\dfrac{1}{\sqrt{2}}$ である．
自分自身の内積の値と比較しよう

```@repl ch011
b1 ⋅ b1
norm(b1)
norm(b1)^2
b1 ⋅ b1
norm(b2)
norm(b2)^2
```

内積から算出した $\cos\theta$から角度 $\theta$ を得るには，関数 `acos()` を用いる．
関数 `acos(r)` は $\cos \theta = r$ となる $\theta$ をラジアンで返す．
関数 `acosd(r)` は，$\theta$ を角度で返す．

これらのベクトルのなす角度を算出しよう．
`a1` と `a2`，および，`b1` と `b2` は直交している．
`a1` と `b1` は，45° をなしている．
`c1` と `c2` は，`120°` をなしている，ことが計算できた．

```@repl ch011
acosd(a1 ⋅ a2)
acosd(b1 ⋅ b2 / norm(b1) / norm(b2))
acosd(a1 ⋅ b1 / norm(a1) / norm(b1))
acosd(c1 ⋅ c2)
```


## ■ タプル

タプル（tuple）は，複数の値をカンマ `,` で区切って並べ，括弧 `()` で囲んだものである．
ベクトルと似たように使えるが，要素を更新することはできない．

```@repl ch011
# 要素 1つのタプル
(1,)
# 要素 2つのタプル
(1, 2)
# 要素 3つのタプル
a = (1, 2, 3)
# タプルの長さ
length(a)
# タプルの要素
a[2]
# 更新はできない
a[2] = 3 # => MethodError
```

関数には，複数の値を返すものがある．このとき，タプルが用いられる．

例えば，`divrem(x,d)` は，`div(x,d)` と `rem(x,d)` の二つの値を返す．

```@repl ch011
divrem(5, 3)
```

タプルを右辺において，複数の変数に同時に代入できる．

```@repl ch011
x, y = (1, 2, 3)
x
y
```


## ■ 行列

要素を `;` で区切って列挙したものを，
大かっこ `[]` で囲むと，行列を作ることができる．

```@repl ch011
a = [11 12 13 14; 21 22 23 24; 31 32 33 34]
```

3行 4列の行列

行列の寸法や要素の数は，以下のように得られる．

```@repl ch011
# 寸法 => タプル
size(a)
# 第1軸 = 列の寸法
size(a, 1)
# 第2軸 = 行の寸法
size(a, 2)
# 全要素数
length(a)
```

[■ ベクトル](@ref)も，1次元の「配列（array）」である．
したがって，同じ関数が用意されている．

```@repl ch011
v = [10,20,30]
# 寸法 => タプル
size(a)
# 第1軸 = 列の寸法
size(a, 1)
# 全要素数
length(a)
```


### ■ 行列の転置

`transpose(a)` は，行列 `a` を転置する．すなわち，行と列を入れ替える．

```@repl ch011
a = [11 12 13 14; 21 22 23 24; 31 32 33 34]
size(a)
b = transpose(a)
size(b)
```

### ▶ 行列のスカラー倍・スカラーの和差

以下，しばらく，2x2 の正方行列を例に説明する．

```@repl ch011
a = [11 12; 21 22]
a * 2
a .+ 2
a .- 2
```

### ▶ 行列に列ベクトルを加減


```@repl ch011
a = [11 12; 21 22]
v = [1, -1]
a .+ v
a .- v
```

### ▶ 行列どうしの加減

```@repl ch011
a = [11 12; 21 22]
b = a * 2
a + b
a - b
```

### ■ 添字を用いた行列の要素の読み書き

行列の添字は，
第1軸（列）と第2軸（行）の番号を，カンマ `,` で区切って並べ，大かっこ `[]` で囲んだものである．

ベクトルと同じように，添字で示された要素の読み出し，
添字で示された要素の書き換えができる．

```@repl ch011
# 添字による要素の読み出し
a[2, 2]
# 行列の要素の更新
a[1, 2] = 30
a
```

## ■ 部分行列

```@repl ch011
a = [11 12 13 14; 21 22 23 24; 31 32 33 34]
# 列を取り出す
a[:, 2]
# 行を取り出す
a[2, :]
# 部分行列
a[1:2, 1:2]
a[2:3, 2:3]
```

### ▶ 行列に入れた点座標で図形を描画する

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)

xys = Float64[1 2 2 1; 1 1 3 1]
@show xys
plot!(xys[1, :], xys[2, :])
xys .+= [0.5, 0.5]
plot!(xys[1, :], xys[2, :])
xlims!(0, 4)
ylims!(0, 4)
plot!()
```


## ■ 行列とベクトルの積

```@repl ch011
a = [11 12; 21 22]
v = [1, -1]
a * v
```

### ▶ 回転行列とベクトルの積

以下の形の行列を，平面空間に対する回転行列という．

```math
R(\theta) =
\begin{bmatrix} \cos\theta & -\sin\theta \\
\sin\theta & \cos \theta \end{bmatrix}
```

回転行列とベクトルの積は，
そのベクトルを，原点の周りに 反時計方向に角 $\theta$ だけ回転する写像に対応する．

$x^{\prime} = R(\theta) x$

```@repl ch011
# 回転行列
r15 = [cosd(15) -sind(15); sind(15) cosd(15)]
xy = [1, 0]
xy = r15 * xy
xy = r15 * xy
```

これらを描こう．軌跡は円を描いた．

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)
plot!(framestyle=:origin)

r15 = [cosd(15) -sind(15); sind(15) cosd(15)]
xy = [1, 0]

for i = 0:20
   global xy
   scatter!([xy[1]], [xy[2]], ms=4, shape=:circle)
   xy = r15 * xy
end

xlims!(-1.2, 1.2)
ylims!(-1.2, 1.2)
plot!()
```


原点以外の点 $c$ の周りで回転する場合は，回転の中心をずらして，

```math
\begin{aligned}
(x^{\prime}-c) & = R(\theta) (x-c) \\
x^{\prime} & = c + R(\theta) (x-c)
\end{aligned}
```

とすればよい．

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)

r15 = [cosd(15) -sind(15); sind(15) cosd(15)]
xy = [1, 0]
c = [ 0.5, 0.5]

for i = 0:20
   global xy
   scatter!([xy[1]], [xy[2]], ms=4, shape=:circle)
   xy = c + r15 * (xy - c)
end

vline!([c[1]]; aux...)
hline!([c[2]]; aux...)

xlims!(-2, 2)
ylims!(-2, 2)
plot!()
```


## ■ 行列と行列の積

```@repl ch011
a = [1 2; 3 4]
b = [5 6; 7 8]
a * b
```

## ▶ 座標を行列に格納した図形を回転する


```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)
plot!(framestyle=:origin)

r15 = [cosd(15) -sind(15); sind(15) cosd(15)]
xys = Float64[1 2 2 1; 1 1 3 1]

for i = 0:20
   global xys
   plot!(xys[1, :], xys[2, :])
   xys = r15 * xys
end

xlims!(-4, 4)
ylims!(-4, 4)
plot!()
```

回転中心をずらしてみる

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)

r15 = [cosd(15) -sind(15); sind(15) cosd(15)]
xys = Float64[1 2 2 1; 1 1 3 1]
c = [1/2, 1/2]

for i = 0:20
   global xys
   plot!(xys[1, :], xys[2, :])
   xys = c .+ r15 * (xys .- c)
end

vline!([c[1]]; aux...)
hline!([c[2]]; aux...)

xlims!(-4, 4)
ylims!(-4, 4)
plot!()
```



## ■ いろいろな行列の生成

### ■ 要素が 0 の行列を作る

* [`Base.zeros` — Function](https://docs.julialang.org/en/v1/base/arrays/#Base.zeros)

関数 `zeros` は，要素が零 $0$ の行列を作るのに使える．
* 関数 `zeros(n,m)` は，要素の型が浮動小数点で，寸法 `(n,m)` の行列を作る．
* 関数 `zeros(T, n,m)` は，要素の型が `T` で，寸法 `(n,m)` の行列を作る．

```@repl ch011
zeros(3, 4) # 要素は浮動小数点
zeros(Float64, 3, 4) # 上と同じ
zeros(Int64, 3, 4) # 要素は整数
```

行列 `a` と同じ寸法を持つ `0` ベクトルを作るには，

```@repl ch011
a = [11 12 13 14; 21 22 23 24; 31 32 33 34]
zeros(Int64, size(a))
zeros(Float64, size(a))
```

関数 `zero.()` を以下のように用いれば
行列 `a` の要素の型と同じ要素の型を持ち，`a` と寸法が等しい `0` ベクトルを作れる．

```@repl ch011
a = [11 12 13 14; 21 22 23 24; 31 32 33 34]
zero.(a)
```


### ■ 要素が 1 の行列を作る

* [`Base.ones` — Function](https://docs.julialang.org/en/v1/base/arrays/#Base.ones)

関数 `ones` は，要素が零 $1$ の行列を作るのに使える．
* 関数 `ones(n,m)` は，要素の型が浮動小数点で，寸法 `(n,m)` の行列を作る．
* 関数 `ones(T, n,m)` は，要素の型が `T` で，寸法 `(n,m)` の行列を作る．

```@repl ch011
ones(3, 4) # 要素は浮動小数点
ones(Float64, 3, 4) # 上と同じ
ones(Int64, 3, 4) # 要素は整数
```

行列 `a` と同じ寸法を持つ `1` 行列を作るには，

```@repl ch011
a = [11 12 13 14; 21 22 23 24; 31 32 33 34]
ones(Int64, size(a))
ones(Float64, size(a))
```

関数 `one.()` を以下のように用いれば
行列 `a` の要素の型と同じ要素の型を持ち，`a` と寸法が等しい `1` 行列を作れる．

```@repl ch011
a = [11 12 13 14; 21 22 23 24; 31 32 33 34]
one.(a)
```


### ■ 対角要素を指定して，正方行列をつくる

```@repl ch011
using LinearAlgebra
Diagonal([1, 2, 3])
```

### ■ 疑似乱数を要素とする行列を作る

```@repl ch011
rand(3, 3)
```

### ■ ベクトルをまとめて行列を作る

```@repl ch011
a = [1, 2]
b = [3, 4]
c = [5, 6]
[a b c]
m = hcat(a, b, c)
size(m)
```

さらに転置をとろう．
```@repl ch011
mt = transpose(m)
size(mt)
```

### ● 内包表現を用いて行列を作る

```@repl ch011
[[t, t .^ 2] for t in [0, 2, 4]]
hcat([[t, t .^ 2] for t in [0, 2, 4]]...)
```

## ▶ 楕円を描く・回転する

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)

ts = 0:pi/18:2pi
xs = 2 * cos.(ts)
ys = sin.(ts)
xys = transpose(hcat(xs, ys))
plot!(xys[1, :], xys[2, :])

xlims!(-3,3)
ylims!(-3,3)
plot!()
```

!!! note
    楕円の座標を含む行列 `xy` を作るのに，以下のように[● 内包表記](@ref)を使ってもよい．

    ```@repl ch011
    xy=hcat([ [2*cos.(t), sin(t)] for t=0:pi/18:2pi]...)
    ```

回転する

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)

r15 = [cosd(15) -sind(15); sind(15) cosd(15)]
ts = 0:pi/18:2pi
xys = transpose(hcat(2 * cos.(ts), sin.(ts)))

for i = 0:5
   global xys
   plot!(xys[1, :], xys[2, :])
   xys = r15 * xys
end

xlims!(-3,3)
ylims!(-3,3)
plot!()
```


## ■ 行列式と逆行列

```@repl ch011
a = [1 2; 2 3]
det(a) # 行列式
inv(a) # 逆行列
a^(-1) # 逆行列
```


## ■ 行列の商（2x2 行列）

行列 $A$とベクトル $x$ に対して，$x=Ay$ を満たすようなベクトル $y$を，$x$を$A$で割った商という．商を求める演算が用意されている．この演算は，$A$の逆行列を計算するよりも，計算コストの上で有利である．

```@repl ch011
a = [1 2; 2 3]
v = [1, 1]
a \ v
w = [3, 5]
a \ w
```

行列 $A$ が正則でない（逆行列が存在しない）場合には，例外が発生する．

```@repl ch011
c = [1 2; 2 4]
det(c)
c \ v # => SingularException
```


同様に，行列 $X$ に対して，$X=AY$ を満たすような行列 $Y$ を，$X$ を $A$ で割った商という．

```@repl ch011
b = [1 3; 1 5]
a \ b
```

## ▶ 楕円を，逆に回転する

行列の商を用いて，楕円を逆回転してみよう．

```@example ch011
closeall() #hide
plot(aspect_ratio=:equal)

r15 = [cosd(15) -sind(15); sind(15) cosd(15)]
ts = 0:pi/18:2pi
xys = transpose(hcat(2 * cos.(ts), sin.(ts)))

for i = 0:5
   global xys
   plot!(xys[1, :], xys[2, :])
   xys = r15 \ xys
end

xlims!(-3,3)
ylims!(-3,3)
plot!()
```


行列式が 0 の行列は，正則ではない

```@repl ch011
a = [1 2; 2 4]
det(a)
v = [1, 1]
# 例外を発生する
a \ v
```

```@repl ch011
# 例外を発生する
inv(a)
```


## ▶ 空間ベクトル：なす角を求める

空間ベクトルとは，寸法 3 のベクトルである．
内積が $0$ なら，それらのベクトルは直交である．

例: 以下の３つのベクトルが，互いに直行することを示せ．

```@repl ch011
using LinearAlgebra
#
a = [1 / 2, 1 / 2 + sqrt(2) / 4, 1 / 2 - sqrt(2) / 4]
b = [-1 / 2, 1 / 2 - sqrt(2) / 4, 1 / 2 + sqrt(2) / 4]
c = [1 / sqrt(2), -1 / 2, 1 / 2]
norm(a)
norm(b)
norm(c)
dot(a, b)
a ⋅ b
b ⋅ c
c ⋅ a
```

二つのベクトルのなす角を求めよ．

```@repl ch011
a = [-3, 1, 2]
b = [2, -3, 1]
ab = a ⋅ b
na = norm(a)
nb = norm(b)
r = ab / na / nb
# ラジアン単位
acos(r)
# 角度単位
acosd(r)
# 一行で書ける
acosd(a ⋅ b / norm(a) / norm(b))
```

### ■ 空間ベクトルの外積

関数 `cross(a,b)` は，空間ベクトル `a` と `b` との外積または「ベクトル積（outer product）」を返す．

中置き演算子 `×` を用いて `a×b` と書く
こともできる． `×` は，バックスラッシュ `\` に times と入力してから，
TABキーを押すことによって入力できる．
かな漢字変換システムで入力できる「✕」 とは，別の文字である．

外積 $a\times b$ の向きは，$a$ から $b$ へ回転したとき，右ねじが進む方向である．

外積 $a\times b$ の大きさは，$a$ と $b$ のなす角 $\theta$ を用いて，以下のように定義される．
これは，ベクトル a と b がなす平行四辺形の面積である．

```math
\left\vert a\times b\right\vert = \left\vert{a}\right\vert \left\vert{b}\right\vert \sin\theta
```

### ▶ 空間座標の基本単位ベクトル

```@repl ch011
a = [1, 0, 0]
b = [0, 1, 0]
c = [0, 0, 1]
cross(a, b)
# a×b = c
a × b
# b×c = a
b × c
# c×a = b
c × a
```

別の正規直交系の例

```@repl ch011
a = [1 / 2, 1 / 2 + sqrt(2) / 4, 1 / 2 - sqrt(2) / 4]
b = [-1 / 2, 1 / 2 - sqrt(2) / 4, 1 / 2 + sqrt(2) / 4]
c = [1 / sqrt(2), -1 / 2, 1 / 2]
# a×b = c
a × b
# b×c = a
b × c
# c×a = b
c × a
```

ベクトル３重積

３つの空間ベクトルに対して，一般に，以下が成り立つ．

```math
{a}\times ({b} \times {c}) = ({a}\cdot{c}){b} - ({a}\cdot {b}){c}
```


例： 具体的なベクトルの例で，上式が成り立つことを示せ．

```@repl ch011
a = [-3, 1, 2]
b = [2, -3, 1]
c = [1, 2, -3]
# 左辺
lhs = a × (b × c)
# 右辺
rhs = (a ⋅ c) * b - (a ⋅ b) * c
```

## ▶ 行列の商（3x3 行列）

行列 `A` と行列（またはベクトル） `Y`に対して，
商 `A\Y` は，$AX-Y$の最小二乗ノルムが最小となる行列（またはベクトル） `X` を返す．
行列 `A` が正則なら，`A` の逆行列を
左から `Y` に乗じた行列（またはベクトル）と一致する．

```@repl ch011
#
a = [-3, 1, 2]
b = [2, -3, 1]
w = [a b]
c = [1, 2, -3]
v = w \ c
w * v - c
```


## ▶ 行列の階数（ランク）

関数 `rank(a)` は，行列 `a` の階数（ランク，rank）を返す．

```@repl ch011
a = [1 / 2, 1 / 2 + sqrt(2) / 4, 1 / 2 - sqrt(2) / 4]
b = [-1 / 2, 1 / 2 - sqrt(2) / 4, 1 / 2 + sqrt(2) / 4]
c = [1 / sqrt(2), -1 / 2, 1 / 2]
v = [a b c]
rank(v)
#
a = [-3, 1, 2]
b = [2, -3, 1]
c = [1, 2, -3]
v = [a b c]
rank(v)
```


## ▼ 行列の固有値・固有ベクトル

```math
Ax = \lambda x
```

```@repl ch011
a = [4 1; 2 3]
#
using LinearAlgebra
# 固有値
lam = eigvals(a)
# 固有ベクトル
evs = eigvecs(a)
evs[:, 1]
evs[:, 2]
# 確認する a x - lambda x == 0 になるべき
a * evs[:, 1] - lam[1] * evs[:, 1]
a * evs[:, 2] - lam[2] * evs[:, 2]
```


## ★ 今回のまとめ

- ベクトルの内積
- 行列の生成
- 行列に対する関数
- 行列とベクトルの演算
- 行列と行列の演算
- 部分行列
- 2次元の回転行列
