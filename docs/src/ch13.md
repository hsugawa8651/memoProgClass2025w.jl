
# [第13回：関数の定義と呼び出し](@id ch13)

## プロットデフォルトの設定13

```@example ch013
using Plots
#gr() #pythonplot()

Plots.reset_defaults()
default(
    framestyle=:box,thickness_scaling=2,
    aspect_ratio=:auto,
    label="",
    legend_foreground_color=nothing,
    legend_background_color=nothing,
    grid=false,xlabel="", ylabel="",
    palette=:Set1_9,
    linewidth=2,
    markershape=:none,
    markersize=2,
    markerstrokewidth=0,
    warn_on_unsupported=true)
```

## ■ 関数の定義と呼び出し

[■ 関数の定義（代入文形式）](@ref) の節で，代入文形式のユーザ関数の定義を紹介した．
この節では，より一般的な扱いを説明する．

より広い意味では，一連のプログラム片に名前を付けたものが，「関数（function）」である．

### ■ 関数の定義と呼び出し：引数がない場合

以下は，`Hello` という文字を印字する関数の定義を示す．

関数定義は，キーワード `function` で始まり，その後に関数名を書く．
呼び出す際に，渡すべき値がなければ，関数名の後に，括弧・閉じ括弧 `()` を書くだけである．

処理を記述した後，キーワード `end` で終わる．

```@repl ch013
# @highlight-start[1]
function hello()
   println("Hello")
end # @highlight-end
```

この関数を呼び出すには，関数名に続けて `()` を書けばよい．

```@repl ch013
hello()
hello()
```

### ■ 関数の定義と呼び出し：引数がある場合

関数を呼び出す際に，渡す値がある場合は，関数名の後に，
括弧 `()` で囲んで，変数名（仮引数，「かり・ひきすう」，parameter）の並びを書く．

```@repl ch013
# @highlight-start[1]
function hello(name)
   print("Hello ")
   println(string(name))
end # @highlight-end
```

この関数を呼び出すには，関数名に続けて `()` に囲んで，具体的な値（実引数「じつ・ひきすう」argument)を書く．
関数の定義の中に出現した仮引数は，実引数の値に置き換えられる．

```@repl ch013
hello("world")
hello("the Earth")
hello()
# 例外: 実引数 2つ
hello("world", "the Earth")
```

関数 `hello` に対して，引数が 0個の場合の定義と，
引数が1個の場合の定義の２つが紐（ひも）ついている．
関数呼び出しにおける引数の数に応じて，対応する関数が実行される．
上の最後の例のように，対応する関数定義が見つからなければ，例外が発生する．


## ▶ 円を描く関数を作る

`PyPlot` パッケージを読み込んだ状況で，円を描く関数を作成する．

引数は，円の中心座標 `cx, cy`，円の半径 `r` ，線の色 `col` とする．

```@example ch013
# @highlight-start[1]
function draw_circle!(o, cx, cy, c, r)
   t = 0:pi/18:2pi
   xs = cx .+ r * cos.(t)
   ys = cy .+ r * sin.(t)
   plot!(o, xs, ys, lc = c)
end # @highlight-end
```

５つの円を描いてみよう．

```@example ch013
using Plots
p = plot(aspect_ratio=:equal, framestyle=:origin)

r = 1
s = 1.1
draw_circle!(p,  0, s, :black, r)
draw_circle!(p, -s, 0, :yellow, r)
draw_circle!(p,-2s, s, :blue, r)
draw_circle!(p,  s, 0, :green, r)
draw_circle!(p, 2s, s, :red,r )
plot!(p)
```


すべての軸を描画しないようにするには，上に続けて `plt[:axis]("off")` を実行すればよい．

```@example ch013
plot!(framestyle=:empty)
```


!!! note
    オリンピックのシンボルマークは，知的財産権が厳しく保護されていることに留意されたい．

## ■ 仮引数の既定値

仮引数に続けて `=値` を書くことで，既定の値を設定できる．
関数呼び出しで，該当する引数を省略して関数を呼び出した場合には，既定の値が用いられる．

関数 `draw_circle` で，
最後の引数 `r` の既定値を `1` に設定した．

```@example ch013
# @highlight-start[1]
function draw_circle!(o, cx, cy, c, r=1)
   t = 0:pi/18:2pi
   xs = cx .+ r * cos.(t)
   ys = cy .+ r * sin.(t)
   plot!(o, xs, ys, lc = c)
end # @highlight-end
```

関数 `draw_circle` を３つの引数で呼び出した場合には，`r=1` として呼び出したとみなされる．

```@example ch013
r = 1
s = 1.1
p = plot()
draw_circle!(p,  0, s, :black)
draw_circle!(p, -s, 0, :yellow)
draw_circle!(p,-2s, s, :blue)
draw_circle!(p,  s, 0, :green)
draw_circle!(p, 2s, s, :red);
p
```

## ■ 関数の戻り値

関数を実行した結果の値を，呼び出し側で使いたい場合は，しばしばある．

関数定義において，最後に評価した値が，関数の戻り値（return-value）となる．

 [■ 関数の定義（代入文形式）](@ref) を用いても，数 `x` を $3$ 倍する関数を定義すると，以下のようになる．

```
triple(x)=3*x
```

これをより一般の形式で書くと，以下のようになる．最後に評価した `3*x` が関数の戻り値となる．

```@repl ch013
# @highlight-start[1]
function triple(x)
   3 * x
end # @highlight-end
```


```@repl ch013
triple(-1)
```


### ■ return キーワード

キーワード `return 式` は関数内で用いられると，直ちに関数から抜ける（戻る）ことを指示する．
この際，式の結果が関数の戻り値となる．

上の関数は，以下のようにも書ける（が，冗長である）．

```
# @highlight-start[1]
function triple(x)
   return 3 * x
end # @highlight-end
```


また，数 `x` の絶対値を求める関数を自前で書いてみると，以下のようになる．

```@repl ch013
# @highlight-start[1]
function myabs(x)
   # @highlight-start[2]
   if x < 0
      return -x
   # @highlight-end
   # @highlight-start[3]
   else
      return x
   end # @highlight-end
end # @highlight-end
```

```@repl ch013
myabs(-1)
```

!!! note
    上の `myabs` は [■ 3項演算子](@ref) を用いて，以下のように書いてもよい．

    ```
    myabs(x)= x<0 ? -x : x
    ```

## ■ 局所変数

関数内部で代入した変数（「局所変数，local variables」）は，その関数内部でのみ生きている．

下の例で，関数 `triple` 内部で代入した `t` は，関数 `triple` のみで生きている．
コマンドラインで定義した変数 `t` は，関数 `triple` の局所変数 `t` とは別物であり，関数呼び出し後も値は変わっていない．
コマンドラインで定義した変数 `t` の型を，「全域変数（global variables）」ともいう．


```@repl ch013
t = 4

# @highlight-start[1]
function triple(x)
   t = 3
   t * x
end # @highlight-end

triple(2)
@show t;
```

## ▶ ヘロンの公式を関数にする

ヘロンの公式（Heron's formula）によれば，
3辺の長さが $a, b, c$ である三角形の面積は

```math
\begin{aligned}
&\sqrt{s(s-a)(s-b)(s-c)}\\
s &=\dfrac{a+b+c}{2}
\end{aligned}
```

で与えられる．

ヘロンの公式を用いて，三角形の面積を返す関数を定義してみる．

三辺 $a, b, c$から三角形を作ることができない場合には，平方根の中身が負になる．
このときは，面積として `0` を返すことにする．

```@repl ch013
# @highlight-start[1]
function heron(a, b, c)
   s = (a + b + c) / 2
   r = s * (s - a) * (s - b) * (s - c)
   # @highlight-start[2]
   if r < 0
      return 0.0
   end # @highlight-end
   sqrt(r)
end # @highlight-end
```

実行してみよう．

```@repl ch013
heron(3, 4, 5)
# 三角形ができない場合
heron(3, 3, 7)
```


## ▼ 方形波のフーリエ級数和を関数にする

方形波をフーリエ級数の和として計算する方法を，[▼ 方形波：フーリエ級数の有限和](@ref) の節で，紹介した．
時刻の配列 `ts` における方形波を，奇数 $n$ までのフーリエ級数和として計算する関数を書いてみる．

```@example ch013
# @highlight-start[1]
function square(ts, n)
   ys = zeros(size(ts))
   # @highlight-start[2]
   for i = 1:2:n
      ys += sin.(i * ts) / i * 4 / pi
   end # @highlight-end
   ys
end # @highlight-end
```

実行してみよう．

```@example ch013
@show square((0:6) * pi / 4, 13);
```

級数和の上限 $n$ を増やして，プロットを描こう．

```@example ch013
using LaTeXStrings
using Plots
plot(legend=:right)

ts = -74*pi/36:pi/36:74*pi/36
plot!(ts, sign.(sin.(ts)), label = "square wave")

# @highlight-start[1]
for n in [5, 9, 13]
   plot!(ts, square(ts, n), label = "up to " * string(n))
end # @highlight-end

yticks!([-1, 0, 1], ["-1", "0", "1"])
xticks!([-2pi, -pi, 0, pi, 2pi], 
   [L"-2\pi", L"-\pi", "0", L"\pi", L"2\pi"])
```


## ◀ 練習：フーリエ級数の和

上の関数 `square` を以下のように改良せよ．

*  $n$ が偶数の場合でも，適切な解釈に基づき動作するようにせよ．
*  $n$ が 3 よりも小さい場合は `n=13` とせよ．

## ▼ Riemann和の計算を関数にする

関数の定積分の近似値を，短冊の面積の和として計算する方法を [▼ Riemann和（繰り返しで加算)](@ref)の節で，紹介した．
関数 `f` ，定積分の範囲 `a, b` ，分割数 `n` を引数として，Rienmann和を計算する関数を書いてみる．

```@example ch013
# @highlight-start[1]
function riemann_sum(f, a, b, n)
   xs = range(a, b, length = n + 1)
   d = (b - a) / n
   s1 = 0
   # @highlight-start[2]
   for i = 1:n
      x = xs[i]
      s1 += g(x) * d
   end # @highlight-end
   s1
end # @highlight-end
```

実行してみよう．

```@example ch013
g(x) = 1 / (1 + x)
a = 0;
b = 1;
@show riemann_sum(g, a, b, 2^10);
```

分割数を増やす．

```@example ch013
# @highlight-start[1]
for m = 8:13
   @show m, riemann_sum(g, a, b, 2^m)
end # @highlight-end
@show log(2);
```

## ◀ 練習：Riemann和の誤差評価

上の例で，相対誤差を描け．

## ◀▼ 練習：Riemann和

上の関数 `riemann_sum` を，以下のように改良せよ．

*  $n < 1$ の場合には，ただちに `0` を返すようにせよ．
*  $n < 1$ の場合には，$n=4$ として，計算せよ．
*  $a > b$ の場合には，上限と下限を交換してから計算せよ．ヒント：変数 $a, b$ の値を交換するには，同時代入 `b, a = a, b` を用いよ．


## ▼ モンテカルロ法による平面図形の面積の近似値を関数にする

平面内のある領域の面積の近似値をモンテカルロ法で求める方法を，[▼ モンテカルロ法による平面図形の面積の推定](@ref) で紹介した．

範囲を表す関数 `f` ，点の数 `n` を引数として，モンテカルロ法で面積の近似値を計算する関数を書いてみる．
関数 `f` は，平面座標 `x,y` を引数にして，`(x,y)` が図形の内部なら真 `true` を，内部でなければ偽 `false` を返すものとする．
なお，図形の範囲は，$x$ 座標，$y$ 座標とも，$0$ から $1$ までの範囲とする．

```@example ch013
# @highlight-start[1]
function montecarlo(f, n)
   s = 0
   # @highlight-start[2]
   for i = 1:n
      x = rand()
      y = rand()
      # @highlight-start[3]
      if f(x, y)
         s += 1
      end # @highlight-end
   end # @highlight-end
   s / n
end # @highlight-end
```

四分円に対して，実行してみよう．

```@example ch013
quadrant(x, y) = x * x + y * y < 1
n = 2^8
@show n, montecarlo(quadrant, n), pi / 4
```

点の数を増やしてみる．

```@example ch013
# @highlight-start[1]
for m = 1:10
   n = 2^m
   @show n, montecarlo(quadrant, n)
end # @highlight-end
```

今度は，二つの不等式 ``y < x`` と ``y < 1 − x`` の両方に囲まれる領域の面積を推定しよう．

```@example ch013
tri1(x, y) = y < x && y < 1 - x
n = 2^8
@show n, montecarlo(tri1, n)
```

点の数を増やす．

```@example ch013
# @highlight-start[1]
for m = 4:12
   local n = 2^m
   @show n, montecarlo(tri1, n)
end # @highlight-end
@show 1 / 4
```

## ◀ 練習：モンテカルロ法による面積の近似値の誤差評価

上の２つの例で，相対誤差を描け．

## ◀▼ 練習：モンテカルロ法による面積の近似値

上の関数 `montecarlo` を，以下のように改良せよ．

*  $n \le 1$ の場合には，$n=2^8$ として，計算せよ．


## ■ 関数から複数の値を返す

[■ タプル](@ref) の小節で，関数 `divrem` のように，複数の値を返す関数があることを紹介した．

* [Multiple Return Values (section)](https://docs.julialang.org/en/v1/manual/functions/#Multiple-Return-Values)

ユーザ関数から複数の値を返すには，関数定義の最後に評価する式において，カンマ `,` で区切って復数の式を書けばよい．
この関数の戻り値は，[■ タプル](@ref) となる．

```@example ch013
function one_two()
   1, 2
end

@show one_two()
```

```@example ch013
x, y = one_two()
@show x, y
```


## ▼ 「はさみうち」法による，方程式の求解を関数にする

「はさみうち」法を用いて，方程式の解の存在範囲を狭めていく方法を，[▼ 「はさみうち」法による，方程式の求解](@ref) で紹介した．

求めるべき方程式 `f` ，解の存在範囲の下限 `a` ，および 上限 `b` を引数として，より狭い解の存在範囲の下限と上限を返す関数を書いてみる．

```@example ch013
function bisect(f, a, b)
   c = (a + b) / 2
   if f(a) * f(c) > 0
      a = c
   else
      b = c
   end
   a, b
end
```

```@example ch013
g(x) = x^3 + 3x^2 - 4 * x - 12
a = -3.2;
b = -2.6;

for i = 1:10
   global a, b
   @show a, b, b - a, g(a), g(b)
   a, b = bisect(g, a, b)
end
```

## ◀ 練習：「はさみうち」法

* 上の例で，解の存在範囲が狭くなる様子を描け．
* 異なる解の存在範囲に対して，「はさみうち」法を実行せよ．


## ■ 引数の型の指定

関数定義の仮引数には `::型` という形式で，仮引数の型を指定できる．
関数呼出しの際，実引数の型と仮引数の型が一致する関数が呼び出される．
この仕組を，多重ディスパッチ（multiple dispatch）という．

型が指定されていない仮引数は，`Any` 型とみなされ，あらゆる実引数の型と一致する．

個別の仮引数に対応する関数は，メソッドと呼ばれる．

例で説明する．

関数 `mytest` を，`Int64` 型の引数に対してだけ定義しよう．

```@repl ch013
function mytest(x::Int64)
   println(string(x) * " is of Int64 type")
end
```

この段階で，`mytest(1)` は上の関数が呼び出されるが，`mytest(1.0)` は呼び出されるべき関数が見つからず，例外が発生する．

```@repl ch013
# 関数が呼び出される
mytest(1)
# 関数が呼び出されない
mytest(1.0)
```

関数 `mytest` を，`Float64` 型の引数に対しても定義する．

```@repl ch013
function mytest(x::Float64)
   println(string(x) * " is of Float64 type")
end

# 今度は，上の関数が呼び出される
mytest(1.0)
```

しかし，`mytest("a")` や `mytest("[1]")` は呼び出されるべき関数が見つからず，例外が発生する．

```@repl ch013
# 関数が呼び出されない
mytest("a")
# 関数が呼び出されない
mytest([1])
```

さらに，仮引数に型を指定しない関数を定義すれば，そちらが呼び出される．

```@repl ch013
function mytest(x)
   println(string(x) * " is neither of Int64 type nor of Float64 type")
end

# 今度は，上の関数が呼び出される
mytest("a")
# 今度は，上の関数が呼び出される
mytest([1])
```

## ▶ 階乗関数を定義する

1 から整数 `n` までの連続する整数の積を，「階乗（factorial）」といい，$n!$ のように書く．

```math
6 ! = 6 \times 5 \times 4 \times 3 \times 2 \times 1 = 720
```

さらに，$0! = 1$ と定義する．

階乗を計算する関数を定義しよう．
階乗関数は整数に対してのみ定義されるから，引数を整数型に限定しよう．$0$ よりも小さい整数に対して，階乗は定義されないが，$1$ を返すことにする．

```@repl ch013
function myfact1(n::Int64)
   n <= 1 && return 1
   r = 1
   for i = 2:n
      r *= i
   end
   r
end
```

計算結果を確かめてみる．

```@repl ch013
for n = 6:-1:-1
   @show n, myfact1(n)
end
```

浮動小数点数や整数ベクトルに対して，関数 `myfact1` は未定義である．

```@repl ch013
# 関数は未定義
myfact1(2.2)
# 関数は未定義
myfact1([6, 3])
```

整数を要素とするベクトルに対して dot記法を用いて `myfact1` を呼び出せば，
各要素に対して関数 `myfact1` を呼び出した値のベクトルが得られる．

```@repl ch013
myfact1.([6, 5, 4])
```

!!! note
    Julia には，階乗関数 `factorial(n)` が組み込まれている．

```@repl ch013
factorial(1)
factorial(2)
factorial(3)
```

## ▶ 再帰

階乗には $n! = n \times (n-1)!$ という性質がある．
すなわち，$n!$ は $(n-1)!$ を用いて定義される．

これをプログラムで書く場合には，関数定義の中で自分自身を呼ぶ，これを再帰（recursion）という．

再帰を用いて，階乗を定義してみよう．

```@repl ch013
function myfact2(n::Int64)
   n <= 1 && return 1
   n * myfact2(n - 1)
end
```


計算結果を確かめよう．

```@repl ch013
for n = 6:-1:-1
   @show n, myfact2(n)
end
```

再帰呼出しを用いる場合には，
計算が必ず終了する条件を設定する必要がある．


## ◀ 練習：再帰：フィボナッチ数

フィボナッチ（Fibonacci）数とは，以下の漸化式で定義される数列である．

```math
\begin{aligned}
F_{0} & = 0,\\
F_{1} & = 1,\\
F_{n+2} &= F_{n+1}+F_{n}
\end{aligned}
```

整数 $n$に対してフィボナッチ数を計算する関数を再帰を用いて定義し，その動作を確かめよ．

フィボナッチ数の最初の 20 個は，以下の通りである．

$0, 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, 144, 233, 377, 610, 987, 1597, 2584, 4181, 6765, 10946,$


また，フィボナッチ数の一般項は，以下の式で与えられることが知られている．合わせて，計算してみよ．

$F_{n} = \dfrac{1}{\sqrt{5}} \left\{ \left(\dfrac{1+\sqrt{5}}{2}\right)^{n} - \left(\dfrac{1-\sqrt{5}}{2}\right)^{n} \right\}$


!!! note
    上で示した階乗やフィボナッチ数の例では，関数が呼ばれるごとに計算を行っており「もったいない」．
    計算結果が一意（一通り，unique）であるなら，計算した結果を記録しておき，同じ引数で再び呼び出されたときには，
    記録から取り出してくれば計算資源が節約できる．このような考え方を「メモ化」という．この本文で説明した範囲でも，
    ベクトルなどを用いて「メモ化」を実装できるであろう．


## ■ 今回のまとめ

- 関数
  - 定義
  - 呼出し
  - 戻り値
  - 仮引数の型
  - 再帰呼出し
- 例題
  - ヘロンの公式
  - フーリエ級数和の関数化
  - モンテカルロ法の関数化
  - はさみうち法の関数化
  - 階乗
  - フィボナッチ数
